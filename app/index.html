<!DOCTYPE html>
<html>
<head>
    <meta charset="utf-8">
    <script id="shader-fs" type="x-shader/x-fragment">
        precision highp float;

        const int MAX_DIRECTIONAL_LIGHTS = 10;

        varying vec3 normal;
        varying vec4 position;

        struct Material {
            vec3 color;
        };

        struct DirectionalLight {
            vec3 direction;
            vec3 diffuseColor;
            vec3 ambientColor;
        };

        struct PointLight {
            vec3 position;
            vec3 ambientColor;
            vec3 diffuseColor;
            float constantAttenuation;
            float linearAttenuation;
            float exponentAttenuation;
        };

        struct SpotLight {
            vec3 position;
            vec3 diffuseColor;
            vec3 direction;
            float cosOuterAngle;
            float cosInnerAngle;
            float range;
        };

        uniform Material material;
        uniform DirectionalLight directionalLight;
        uniform PointLight pointLight;
        uniform SpotLight spotLight;

        vec4 getDirectionalLightColor(const DirectionalLight directionalLight, vec3 normal) {
            float diffuse = max(0.0, dot(normal, -directionalLight.direction));
            return vec4(directionalLight.ambientColor + diffuse * directionalLight.diffuseColor, 1.0);
        }

        vec4 getPointLightColor(const PointLight pointLight, vec3 position, vec3 normal) {
            vec3 positionToLight = position - pointLight.position;
            float distance = length(positionToLight);
            positionToLight = normalize(positionToLight);

            float diffuse = max(0.0, dot(normal, -positionToLight));
            float attenuation = pointLight.constantAttenuation + pointLight.linearAttenuation * distance + pointLight.exponentAttenuation * distance * distance;

            return vec4(pointLight.ambientColor + pointLight.diffuseColor * diffuse / attenuation, 1.0);
        }

        vec4 getSpotLightColor(const SpotLight spotLight, vec3 position, vec3 normal) {
            vec3 L = spotLight.position - position;
            float distance = length(L);
            L = normalize(L);

            float spotEffect = smoothstep(spotLight.cosOuterAngle, spotLight.cosInnerAngle, dot(L, -spotLight.direction));
            float attenuation = smoothstep(spotLight.range, 0.0, distance);
            float diffuse = max(dot(normal, L), 0.0);

            return vec4(diffuse * spotLight.diffuseColor * spotEffect * attenuation, 1.0);
        }

        void main(void) {
            vec4 color = getDirectionalLightColor(directionalLight, normal);
            color += getPointLightColor(pointLight, position.xyz, normal);
            color += getSpotLightColor(spotLight, position.xyz, normal);
            gl_FragColor = vec4(material.color, 1.0) * color;
        }
    </script>

    <script id="shader-vs" type="x-shader/x-vertex">
        attribute vec3 vertexPosition;
        attribute vec3 vertexNormal;
        attribute vec4 vertexColor;

        uniform mat4 modelMatrix;
        uniform mat4 viewMatrix;
        uniform mat4 projectionMatrix;
        uniform mat3 normalMatrix;

        varying vec3 normal;
        varying vec4 position;
        varying vec4 eye;

        void main(void) {
            normal = normalMatrix * vertexNormal;
            position = modelMatrix * vec4(vertexPosition, 1.0);
            eye = viewMatrix * position;
            gl_Position = projectionMatrix * eye;
        }
    </script>
    <script type="text/javascript" src="../lib/jquery-2.1.0.min.js"></script>
    <script type="text/javascript" src="../dist/webgl3d.js"></script>
    <script type="text/javascript" src="app.js"></script>
</head>
<style>
    body {
        margin: 0;
    }
</style>
<body onload="main()">
<canvas id="gl-canvas" width="1000" height="600">
    Your browser doesn't appear to support the HTML5 <code>&lt;canvas&gt;</code> element.
</canvas>
</body>
</html>